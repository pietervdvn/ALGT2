import ALGT.Builtins

 STFL
******

# A simply typed functional language; mainly used for testing purposes

 Syntax
========


baseType	::= "Bool" | "Int" | "(" type ")"
type		::= baseType "->" type | baseType

bool	::= "True" | "False"

var	::= Identifier

lambda	::= "(" "\\" var ":" type "." expr ")"

canon	::= bool | number | var | lambda

baseExpr ::= canon | "(" expr ")"

intOp	::= "+" 
boolOp	::= "&" 
sameOp	::= "="

op	::= intOp | boolOp | sameOp

expr	::= baseExpr op expr | "If" expr "Then" expr "Else" expr | baseExpr

typingEnv	::= var ":" type typingEnv | "[]"


 Functions
===========

not	: bool -> bool
not("True")	= "False"
not("False")	= "True"


# Searches the typing environment
search	: var * typingEnv -> type
search(x, x ":" t _)
	= t
search(x, _ _ _ te)
	= search(x, te)


typeEq	: type * type -> type
typeEq(t, t)
	= t
typeEq("(" t ")", t)
	= t
typeEq(t, "(" t ")")
	= t
typeEq(ta0 "->" tr0, ta1 "->" tr1)
	= typeEq(ta0, ta1) "->" typeEq (tr0, tr1)


 Relations
===========

(→)	: expr (in) * expr (out)

(::) : expr (in) * type (out) ; Pronounced as "empty env"
(⊢) : typingEnv (in) * expr (in) * type (out)

 Rules
========


 (⊢) "[]", expr, type
---------------------- [ Type in empty environment ]
 (::) expr, type


 expr : bool
---------------------- [TTrue]
 (⊢) _, expr, "Bool"


 expr : number
---------------------- [TNumber]
 (⊢) _, expr, "Int"


 x : var
----------------------------- [TVar]
 (⊢) env, x, search(x, env)


 (⊢) x ":" typeDom env, expr, typeCod
-------------------------------------------------------------------- [TLambda]
 (⊢) env, "(" "\\" x ":" typeDom "." expr ")", typeDom "->" typeCod


 (⊢) env, expr, type
------------------------------ [TPar]
 (⊢) env, "(" expr ")", type


 op : boolOp	(⊢) env, expr0, "Bool"		(⊢) env, expr1, "Bool"		
---------------------------------------------------------------------------- [TBoolInfix]
 (⊢) env, expr0 op expr1, "Bool"



 (⊢) env, expr0, "Int"	(⊢) env, expr1, "Int"		op : intOp
---------------------------------------------------------------------------- [TIntInfix]
 (⊢) env, expr0 op expr1, "Int"


 (⊢) env, expr0, t0	(⊢) env, expr1, t1
-------------------------------------------- [TSameInfix]
 (⊢) env, expr0 op expr1, typeEq(t0, t1)

------------------------------------------------------------------------- [TIf]
 (⊢) env, "If" cond "Then" e0 "Else" e1, typeEq(t0, t1)

